plugins {
	id 'fabric-loom' version '0.5-SNAPSHOT'
	id "com.modrinth.minotaur" version "1.1.0"
	id "com.matthewprenger.cursegradle" version "1.4.0"
}

repositories {
	maven {
		url = "https://maven.fabricmc.net"
	}
	maven {
		url = "https://minecraft.guntram.de/maven/"
	}
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

ext.Versions = new Properties()
Versions.load(file("Versionfiles/mcversion-1.16.5.properties").newReader())

archivesBaseName = "easiervillagertrading"
ext.projectVersion = "1.5.4"

version = "${Versions['minecraft_version']}-fabric${Versions['fabric_versiononly']}-${project.projectVersion}"

minecraft {
    refmapName = "EasierVillagerTrading-refmap.json";
}

processResources {
	inputs.property "version", project.version

    from(sourceSets.main.resources.srcDirs) {
		include "fabric.mod.json"
		expand "version": project.version
	}
	from(sourceSets.main.resources.srcDirs) {
		exclude "fabric.mod.json"
	}
}

dependencies {
    minecraft  "com.mojang:minecraft:${Versions['minecraft_version']}"
    mappings   "net.fabricmc:yarn:${Versions['yarn_mappings']}:v2"
    modCompile "net.fabricmc:fabric-loader:${Versions['loader_version']}"
    modCompile "net.fabricmc.fabric-api:fabric-api:${Versions['fabric_version']}"
    modCompile "io.github.prospector:modmenu:${Versions['modmenu_version']}"
    modImplementation "de.guntram.mcmod:GBfabrictools:${Versions['gbfabrictools_version']}"
    include    "de.guntram.mcmod:GBfabrictools:${Versions['gbfabrictools_version']}"    
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

jar {
	from "LICENSE"
}

import com.modrinth.minotaur.TaskModrinthUpload

task publishModrinth (type: TaskModrinthUpload){

    token = System.getenv("MODRINTH_TOKEN")
    projectId = 'ChscgScW'
    versionNumber = project.version
    versionName = project.archivesBaseName
    releaseType = 'release'
    uploadFile = remapJar // This is the java jar task
    addGameVersion("${Versions['minecraft_version']}")
    addLoader('fabric')
}

curseforge {
    apiKey = System.getenv("CURSEFORGE_TOKEN")
    if (apiKey == null || apiKey == "") {
        apiKey = "DUMMY"        // or CurseGradle will yell even when not used
    }
    project {
        id = '261605'
        releaseType = 'release'
        addGameVersion("${Versions['minecraft_version']}")
        addGameVersion("Java 8")
        addGameVersion("Fabric")

        mainArtifact(remapJar)
    }
    options {
        forgeGradleIntegration = false
    }
}

